//created on: 29-oct-2025
package com.sample.rules

//list any import classes here.


import clasesProyecto.Resultado;
import clasesProyecto.EntradaElectro;
import clasesProyecto.Onda;
import clasesProyecto.ContadorCiclos;
import enums.Enfermedades;
import enums.Tipos_Ondas;
import java.util.List;
import java.util.ArrayList;
import java.util.Comparator;
import clasesProyecto.Intervalo;


rule "Asignar Ciclo en orden"
    salience 100 
when
    $contador: ContadorCiclos($cicloActual: cicloActual)
    $onda: Onda(ciclo == 0, $inicio: inicio,tipo != Tipos_Ondas.P)
    not ( Onda(ciclo == 0, inicio < $inicio ))
then
    modify($onda) {
        setCiclo($cicloActual);
    }

end
	
rule "Si es OndaP aumentar los ciclos"
	salience 100
	when
		$ondaP: Onda(tipo == Tipos_Ondas.P,ciclo == 0, $inicio: inicio)
		$contador: ContadorCiclos( $cicloActual: cicloActual)
	then
		int nuevoCiclo = $cicloActual+1;
		modify($contador){
			setCicloActual(nuevoCiclo);
		}
		modify($ondaP){
			setCiclo(nuevoCiclo);
		}
end 


rule "Calcular numCiclos"
	salience 100
	when
		$r: Resultado(numCiclos == 0)
		$ondas: List(size > 0) from collect(Onda())
	then
		float numCiclos = (float) Math.ceil($ondas.size() / 5.0f); 
		modify($r) {
			setNumCiclos(numCiclos);
		}
		System.out.println("Numero de ciclos calculado: " + $r.getNumCiclos());
end



rule "Calcular ritmoCardiaco"
	salience 100
	when
		$r: Resultado(numCiclos > 0, ritmoCardiaco == 0)
		$ondas: List(size > 0) from collect(Onda());
	then
		List<Onda> ondas = (List<Onda>) $ondas;
		ondas.sort(Comparator.comparing(onda -> onda.getInicio()));
		

		int resto = ondas.size() % 5;
		
		if (resto != 0) {
    		for(int i = 0; i < resto; i++) {
    			ondas.remove(ondas.size() -1);
    		}
    	}
		
		float inicioPrimeraOnda = ondas.get(0).getInicio();
		float finalUltimaOnda = ondas.get(ondas.size() -1).getFin();
		
		float duracionTotal = finalUltimaOnda - inicioPrimeraOnda;
		float duracionMedia = duracionTotal / $r.getNumCiclos();
		float ritmoCardiaco = 60000.f / duracionMedia;
		
		modify ($r) {
			setRitmoCardiaco(ritmoCardiaco);
		}
		
		System.out.println("Ritmo Cardiaco calculado: " + $r.getRitmoCardiaco());
end

rule "Calcular intervalo QT"
	salience 10
	when 
		$ondaQ: Onda(tipo == Tipos_Ondas.Q,$inicioQ: inicio,$ciclo: ciclo)
		$ondaT: Onda(tipo == Tipos_Ondas.T,ciclo == $ciclo,$finT: fin)
		//$intervaloQT: Intervalo(tipo == "QT",$cicloInter: ciclo)
		not(Intervalo(tipo == "QT", ciclo == $ciclo))
	then
		float valorQT = $finT - $inicioQ;
		
		insert( new Intervalo("QT", valorQT, $ciclo) );
		//System.out.println(valorQT + " es la duracion del intervaloQT, del ciclo" + $ciclo);
end

rule "Calcular intervalo ST"
	salience 10
	when 
		$ondaS: Onda(tipo == Tipos_Ondas.S,$inicioS: inicio,$ciclo: ciclo, $picoS: pico)
		$ondaT: Onda(tipo == Tipos_Ondas.T,ciclo == $ciclo,$finT: fin, $picoT: pico)
		//$intervaloQT: Intervalo(tipo == "ST",$cicloInter: ciclo)
		not(Intervalo(tipo == "ST", ciclo == $ciclo))
	then
		float valorST = $finT - $inicioS;
		float picoST = $picoT - $picoS;
		
		insert( new Intervalo("ST", valorST, $ciclo) );
end

rule "Calcular complejo QRS"
	salience 10
	when 
		$ondaQ: Onda(tipo == Tipos_Ondas.Q,$ciclo: ciclo,$inicioQ: inicio)
		$ondaS: Onda(tipo == Tipos_Ondas.S,ciclo == $ciclo,$finS: fin)
		not(Intervalo(tipo == "QRS", ciclo == $ciclo))
	then
		float valorQRS = $finS - $inicioQ;
		
		insert( new Intervalo("QRS", valorQRS, $ciclo) );
end


rule "Calcular intervalo RR"
	salience 10
	when 
		$ondaR1: Onda(tipo == Tipos_Ondas.R,$inicioR1: inicio,$cicloR1: ciclo)
		$ondaR2: Onda(tipo == Tipos_Ondas.R,$inicioR2: inicio,ciclo == $cicloR1+1)
		not Intervalo(tipo == "RR", ciclo == $cicloR1)
	then
		float valorRR = $inicioR2 - $inicioR1;
		
		insert( new Intervalo("RR", valorRR, $cicloR1) );
end

//
//
//				ENFERMEDADES
//
//
		
rule "Detectar Taquicardia"
	when 
		$r: Resultado (ritmoCardiaco > 100 && !enfermedad.contains(Enfermedades.TAQUICARDIA)) 
	then
		modify ($r) {
			addEnfermedad(Enfermedades.TAQUICARDIA);
		}
		System.out.println("Detectada posible taquicardia. Ritmo cardiaco alto: " + $r.getRitmoCardiaco());
end


rule "Detectar Bradicardia"
	when 
		$r: Resultado (ritmoCardiaco < 60 && !enfermedad.contains(Enfermedades.BRADICARDIA))
	then
		modify($r) {
			addEnfermedad(Enfermedades.BRADICARDIA);
		}
		System.out.println("Detectada posible bradicardia. Ritmo cardiaco bajo: " + $r.getRitmoCardiaco());
end

rule "Detectar Hipocalcemia"
	when
	$r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.HIPOCALCEMIA))
	$intervaloQT: Intervalo(tipo == "QT",duracion>450)
	then
	
	modify ($r) {
			addEnfermedad(Enfermedades.HIPOCALCEMIA);
			}
	System.out.println("Detectada posible Hipocalcemia en el ciclo: " + $intervaloQT.getCiclo());	
	System.out.println(" - Duracion del intervalo QT superior a 450ms -> " + $intervaloQT.getDuracion() + "ms");
end

rule "Detectar Hipopotasemia"
    when
        $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.HIPOPOTASEMIA))
        $ondaT: Onda( tipo == Tipos_Ondas.T, $picoT: pico, $ciclo: ciclo )
        $ondaR: Onda( tipo == Tipos_Ondas.R,ciclo == $ciclo,$picoR: pico)
        $ondaS: Onda(tipo == Tipos_Ondas.S, ciclo == $ciclo, $picoS: pico)
        eval( $picoT < -10.0                         
            && ($picoT < $picoS)                     
            && (Math.abs($picoT / $picoR) > 1) )    
    then

    modify ($r) {
            addEnfermedad(Enfermedades.HIPOPOTASEMIA);
            }
    System.out.println("Detectada posible Hipopotasemia en el ciclo: " + $ondaT.getCiclo());
    System.out.println(" - Pico de la Onda T inferior a -10: " + $ondaT.getPico());
    System.out.println(" - Pico de la Onda T menor que pico de la Onda S: " + $ondaT.getPico() + " < " + $ondaS.getPico());
    System.out.println(" - Pico de la Onda T (en valor absoluto) mucho mayor al pico de la Onda R. Onda T: " + Math.abs($ondaT.getPico()) + "; " +
    																		"Onda R: " + $ondaR.getPico() + "; " +
    																		"Division: " + Math.abs($picoT / $picoR) + " (>1)");
end


rule "Detectar Isquemia Coronaria"
    when
        $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.ISQUEMIA_CORONARIA))
        $ondaS: Onda(tipo == Tipos_Ondas.S, $ciclo: ciclo, $picoS: pico)
        $ondaT: Onda(tipo == Tipos_Ondas.T, ciclo == $ciclo, $picoT: pico)
        eval (($picoT - $picoS) <= -5 && !($picoT < -10))                
    then
        modify ($r) {
                addEnfermedad(Enfermedades.ISQUEMIA_CORONARIA);
                }
        System.out.println("Detectada posible Isquemia Coronaria en el ciclo: " + $ciclo);
        System.out.println(" - Diferencia entre pico de T y S menor o igual a '-5 mV': " + ($ondaT.getPico() - $ondaS.getPico()) + " mV");
        System.out.println(" - Pico de la onda T menor a '-10 mv': " + $ondaT.getPico() + " mV");
end


rule "Detectar Infarto Agudo de Miocardio"
    when
        $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.INFARTO_AGUDO_DE_MIOCARDIO))
        $ondaS: Onda(tipo == Tipos_Ondas.S, $ciclo: ciclo, $picoS: pico)
        $ondaT: Onda(tipo == Tipos_Ondas.T, ciclo == $ciclo, $picoT: pico)
        $intervaloQRS: Intervalo(tipo == "QRS",ciclo == $ciclo, $duracion: duracion)
        $ondaQ: Onda(tipo == Tipos_Ondas.Q, ciclo == $ciclo, $picoQ: pico)
        $ondaR: Onda(tipo == Tipos_Ondas.R, ciclo == $ciclo, $picoR: pico)
        eval(((($picoT + $picoS) >= 0.2) && ($picoR > 1.0)) || (($picoQ < -0.1 && $duracion >= 40 && (Math.abs($picoQ) / $picoR) >= 0.25))
         )
    then
        modify ($r) {
                addEnfermedad(Enfermedades.INFARTO_AGUDO_DE_MIOCARDIO);
                }
        System.out.println("Detectada posible Infarto Agudo de Miocardio en el ciclo: " + $ciclo);
        
        if (($picoT + $picoS) >= 0.2 && ($picoR > 1.0)) {
        	System.out.println(" - Suma de pico Onda T y pico Onda S mayor o igual a 0.2: " + ($picoT + $picoS) + " mV");
        	System.out.println(" - Pico de onda R superior a 1: " + $ondaR.getPico() + " mV");
        }
        
        if (($picoQ < -0.1 && $duracion >= 40 && (Math.abs($picoQ) / $picoR) >= 0.25)) {
        	    System.out.println("Onda Q patologicas: Pico menor a -0.1: " + $ondaQ.getPico() + "; Duracion mayor o igual a 40: " + 
        		$ondaQ.getDuracion() + " ms; Pico de Q (en valor absoluto) entre pico R mayor a 0.25: " + (Math.abs($picoQ) / $picoR));
        }
end




rule "Calcular promedio RR"
salience 5
when
    $intervalosRR: List(size > 0) from collect(Intervalo(tipo == "RR"))
    $r: Resultado()
    not( Intervalo(tipo == "PROMEDIO_RR") )  
then
    double suma = 0;
    for(int i = 0; i < $intervalosRR.size(); i++) {
        suma += ((Intervalo)$intervalosRR.get(i)).getDuracion();
    }
    double promedio = suma / $intervalosRR.size();
    insert(new Intervalo("PROMEDIO_RR", (float)promedio, 0));
end


rule "Detectar ContracciÃ³n Ventricular Prematura"
when
    $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA))
    $intervaloQRS: Intervalo(tipo == "QRS", $ciclo: ciclo, $valorQRS: duracion)
    $intervaloRR: Intervalo(tipo == "RR", ciclo == $ciclo, $valorRR: duracion)
    eval($valorQRS <= 70 && $valorRR >= 600 && $valorRR <= 700)    
then
    modify ($r) {
        addEnfermedad(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA);
    }
    System.out.println("Detectada Contraccion Ventricular Prematura en ciclo " + $ciclo);
    System.out.println(" - Duracion del complejo QRS menor o igual a 70ms: " + $intervaloQRS.getDuracion() + "ms");
    System.out.println(" - Duracion del intervalo RR en el rango 600-700ms: " + $intervaloRR.getDuracion() + "ms");
end


