//created on: 29-oct-2025
package com.sample.rules

//list any import classes here.


import clasesProyecto.Resultado;
import clasesProyecto.EntradaElectro;
import clasesProyecto.Onda;
import clasesProyecto.ContadorCiclos;
import enums.Enfermedades;
import enums.Tipos_Ondas;
import java.util.List;
import java.util.ArrayList;
import java.util.Comparator;
import clasesProyecto.Intervalo;


//duda
//a mitad de la lectura pueden haber ciclos incompletos
//las ondas nos van entrando en orden


// regla para calcular número de ciclos 
//casting necesario a float en numCiclos porque el MathCeil (redondeo hacia arriba) devulve double
//collect coge todas las ondas existentes y, si es hay alguna onda, crea una lista temporal $ondas con esas ondas

rule "Asignar Ciclo en orden"
    salience 100 
when
    $contador: ContadorCiclos($cicloActual: cicloActual)
    $onda: Onda(ciclo == 0, $inicio: inicio,tipo != Tipos_Ondas.P)
    not ( Onda(ciclo == 0, inicio < $inicio ))
then
    modify($onda) {
        setCiclo($cicloActual);
    }
    //System.out.println("ola");
    //System.out.println($onda);
    //System.out.println($cicloActual);
end
	
rule "Si es OndaP aumentar los ciclos"
	salience 100
	when
		$ondaP: Onda(tipo == Tipos_Ondas.P,ciclo == 0, $inicio: inicio)
		$contador: ContadorCiclos( $cicloActual: cicloActual)
	then
		int nuevoCiclo = $cicloActual+1;
		modify($contador){
			setCicloActual(nuevoCiclo);
		}
		modify($ondaP){
			setCiclo(nuevoCiclo);
		}
end 
rule "Calcular numCiclos"
	salience 100
	when
		$r: Resultado(numCiclos == 0)
		$ondas: List(size > 0) from collect(Onda())
	then
		float numCiclos = (float) Math.ceil($ondas.size() / 5.0f); 
		modify($r) {
			setNumCiclos(numCiclos);
		}
		System.out.println("Numero de ciclos calculado: " + $r.getNumCiclos());
end


// regla para calcular el ritmo cardiaco
//casting necesario a List para que deje aplicar getInicio y getFin
//necesario hacer un sort porque al parecer las Ondas no estan en orden en $ondas
// (y por tanto tampoco en ondas tras el casting) y salen valores extraños

rule "Calcular ritmoCardiaco"
	salience 100
	when
		$r: Resultado(numCiclos > 0, ritmoCardiaco == 0)
		$ondas: List(size > 0) from collect(Onda());
	then
		List<Onda> ondas = (List<Onda>) $ondas;
		ondas.sort(Comparator.comparing(onda -> onda.getInicio()));
		

//		int resto = ondas.size() % 4;
//		
//		if (resto != 0) {
//    		for(int i = 0; i < resto; i++) {
//    			ondas.remove(ondas.size() -1);
//    		}
//    	}
		
		float inicioPrimeraOnda = ondas.get(0).getInicio();
		float finalUltimaOnda = ondas.get(ondas.size() -1).getFin();
		
		float duracionTotal = finalUltimaOnda - inicioPrimeraOnda;
		float duracionMedia = duracionTotal / $r.getNumCiclos();
		float ritmoCardiaco = 60000.f / duracionMedia;
		
		modify ($r) {
			setRitmoCardiaco(ritmoCardiaco);
		}
		
		System.out.println("Ritmo Cardiaco calculado: " + $r.getRitmoCardiaco());
end

rule "Calcular intervalo QT"
	salience 10
	when 
		$ondaQ: Onda(tipo == Tipos_Ondas.Q,$inicioQ: inicio,$ciclo: ciclo)
		$ondaT: Onda(tipo == Tipos_Ondas.T,ciclo == $ciclo,$finT: fin)
		//$intervaloQT: Intervalo(tipo == "QT",$cicloInter: ciclo)
		not(Intervalo(tipo == "QT", ciclo == $ciclo))
	then
		float valorQT = $finT - $inicioQ;
		
		insert( new Intervalo("QT", valorQT, $ciclo) );
		//System.out.println(valorQT + " es la duracion del intervaloQT, del ciclo" + $ciclo);
end

rule "Calcular intervalo ST"
	salience 10
	when 
		$ondaS: Onda(tipo == Tipos_Ondas.S,$inicioS: inicio,$ciclo: ciclo, $picoS: pico)
		$ondaT: Onda(tipo == Tipos_Ondas.T,ciclo == $ciclo,$finT: fin, $picoT: pico)
		//$intervaloQT: Intervalo(tipo == "ST",$cicloInter: ciclo)
		not(Intervalo(tipo == "ST", ciclo == $ciclo))
	then
		float valorST = $finT - $inicioS;
		float picoST = $picoT - $picoS;
		
		insert( new Intervalo("ST", valorST, $ciclo) );
		//System.out.println(valorST + " es la duracion del intervaloST, del ciclo" + $ciclo);
		//System.out.println(picoST + " es pico del intervaloST, del ciclo" + $ciclo);
end

rule "Calcular complejo QRS"
	salience 10
	when 
		$ondaQ: Onda(tipo == Tipos_Ondas.Q,$ciclo: ciclo,$inicioQ: inicio)
		$ondaS: Onda(tipo == Tipos_Ondas.S,ciclo == $ciclo,$finS: fin)
		//$intervaloQRS: Intervalo( tipo == "QRS",$cicloInter: ciclo)
		not(Intervalo(tipo == "QRS", ciclo == $ciclo))
	then
		float valorQRS = $finS - $inicioQ;
		
		insert( new Intervalo("QRS", valorQRS, $ciclo) );
		//System.out.println(valorQRS + " es la duracion del intervaloQRS, del ciclo" + $ciclo);
end


rule "Calcular intervalo RR"
	salience 10
	when 
		$ondaR1: Onda(tipo == Tipos_Ondas.R,$inicioR1: inicio,$cicloR1: ciclo)
		$ondaR2: Onda(tipo == Tipos_Ondas.R,$inicioR2: inicio,ciclo == $cicloR1+1)
		not Intervalo(tipo == "RR", ciclo == $cicloR1)
	then
		//System.out.println("fin: " + $finalR2 + "inicio: " + $inicioR1);
		float valorRR = $inicioR2 - $inicioR1;
		
		insert( new Intervalo("RR", valorRR, $cicloR1) );
		//System.out.println(valorRR + " es la duracion del intervaloRR, del ciclo" + $cicloR1);
end

//
//
//				ENFERMEDADES
//
//
		
rule "Detectar Taquicardia"
	when 
		$r: Resultado (ritmoCardiaco > 100 && !enfermedad.contains(Enfermedades.TAQUICARDIA)) 
	then
		modify ($r) {
			addEnfermedad(Enfermedades.TAQUICARDIA);
		}
		System.out.println("Detectada posible taquicardia. Ritmo cardiaco alto: " + $r.getRitmoCardiaco());
end


rule "Detectar Bradicardia"
	when 
		$r: Resultado (ritmoCardiaco < 60 && !enfermedad.contains(Enfermedades.BRADICARDIA))
	then
		modify($r) {
			addEnfermedad(Enfermedades.BRADICARDIA);
		}
		System.out.println("Detectada posible bradicardia. Ritmo cardiaco bajo: " + $r.getRitmoCardiaco());
end

rule "Detectar Hipocalcemia"
	when
	$r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.HIPOCALCEMIA))
	$intervaloQT: Intervalo(tipo == "QT",duracion>450)
	then
	
	modify ($r) {
			addEnfermedad(Enfermedades.HIPOCALCEMIA);
			}
	System.out.println("Detectada posible Hipocalcemia en el ciclo: " + $intervaloQT.getCiclo());	
end

rule "Detectar Hipopotasemia"
    when
        $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.HIPOPOTASEMIA))
        $ondaT: Onda( tipo == Tipos_Ondas.T, $picoT: pico, $ciclo: ciclo )
        $ondaR: Onda( tipo == Tipos_Ondas.R,ciclo == $ciclo,$picoR: pico)
        $ondaS: Onda(tipo == Tipos_Ondas.S, ciclo == $ciclo, $picoS: pico)
        eval( $picoT < -10.0                         //Hipopotasemia tipica Onda T muy negativa
            && ($picoT < $picoS)                     //Intervalo ST descendiente
            && (Math.abs($picoT / $picoR) > 1) )    //T muy grande comparada con R, para diferenciarlo de isquemia
    then

    modify ($r) {
            addEnfermedad(Enfermedades.HIPOPOTASEMIA);
            }
    System.out.println("Detectada posible Hipopotasemia en el ciclo: " + $ondaT.getCiclo());
end


rule "Detectar Isquemia Coronaria"
    when
        $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.ISQUEMIA_CORONARIA))
        $ondaS: Onda(tipo == Tipos_Ondas.S, $ciclo: ciclo, $picoS: pico)
        $ondaT: Onda(tipo == Tipos_Ondas.T, ciclo == $ciclo, $picoT: pico)
        //$ondaR: Onda(tipo == Tipos_Ondas.R, ciclo == $ciclo, $picoR: pico)
        eval (($picoT - $picoS) <= -5            //Haya desnivelacion
                //&& ($picoT - $picoR) <= -5.0    //hacemos una desnivelacion de R a T, pero creo que podria hacerse con S y T
                && !($picoT < -10))                //Que el pico T no sea demasiado bajo
    then
        modify ($r) {
                addEnfermedad(Enfermedades.ISQUEMIA_CORONARIA);
                }
        System.out.println("Detectada posible ISQUEMIA_CORONARIA en el ciclo: " + $ciclo);
end


rule "Detectar Infarto Agudo de Miocardio"
    //salience 5
    when
        $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.INFARTO_AGUDO_DE_MIOCARDIO))
        $ondaS: Onda(tipo == Tipos_Ondas.S, $ciclo: ciclo, $picoS: pico)
        $ondaT: Onda(tipo == Tipos_Ondas.T, ciclo == $ciclo, $picoT: pico)
        $intervaloQRS: Intervalo(tipo == "QRS",ciclo == $ciclo, $duracion: duracion)
        $ondaQ: Onda(tipo == Tipos_Ondas.Q, ciclo == $ciclo, $picoQ: pico)
        $ondaR: Onda(tipo == Tipos_Ondas.R, ciclo == $ciclo, $picoR: pico)
        //eval ((($picoT + $picoS) >= 0.5) || $picoT < 0 || $duracion >= 40 || $picoQ > 0.1 || ($picoQ/$picoR) > 0.25)
        eval(((($picoT + $picoS) >= 0.2) && ($picoR > 1.0))
         ||
         // 2. Ondas Q patológicas
         (($picoQ < -0.1 && $duracion >= 40 && (Math.abs($picoQ) / $picoR) >= 0.25))
         //||
         // 3. T invertida profunda post IAM
         //($picoT < -0.5 && $duracion >= 40))
         )
    then
        modify ($r) {
                addEnfermedad(Enfermedades.INFARTO_AGUDO_DE_MIOCARDIO);
                }
        System.out.println("Detectada posible INFARTO_AGUDO_DE_MIOCARDIO en el ciclo: " + $ciclo);
end




rule "Calcular promedio RR"
salience 5
when
    $intervalosRR: List(size > 0) from collect(Intervalo(tipo == "RR"))
    $r: Resultado()
    not( Intervalo(tipo == "PROMEDIO_RR") )  // Usamos un intervalo especial para el promedio
then
    double suma = 0;
    for(int i = 0; i < $intervalosRR.size(); i++) {
        suma += ((Intervalo)$intervalosRR.get(i)).getDuracion();
    }
    double promedio = suma / $intervalosRR.size();
    insert(new Intervalo("PROMEDIO_RR", (float)promedio, 0));
    //System.out.println("Promedio RR calculado: " + promedio + "ms");
end


rule "Detectar Contracción Ventricular Prematura"
when
    $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA))
    $intervaloQRS: Intervalo(tipo == "QRS", $ciclo: ciclo, $valorQRS: duracion)
    $intervaloRR: Intervalo(tipo == "RR", ciclo == $ciclo, $valorRR: duracion)
    eval($valorQRS <= 70 && $valorRR >= 600 && $valorRR <= 700)    //DURA SIEMPRE 50-70 / Y 650-690
then
    modify ($r) {
        addEnfermedad(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA);
    }
    System.out.println("Detectada CONTRACCION_VENTRICULAR_PREMATURA en ciclo " + $ciclo);
end


///////////////////////////MALO///////////////////////////////////////////////



/*



rule "Detectar Contracción Ventricular Prematura"
	when
		$r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA))
		$intervaloQRS: Intervalo(tipo == "QRS",$cicloQRS: ciclo,$valorQRS: duracion)
		$intervaloRR: Intervalo(tipo == "RR", $cicloQRS == ciclo,$valorRR: duracion)
		eval($valorQRS > 120 || $valorRR > 0.8*690)
	then
	
		modify ($r) {
				addEnfermedad(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA);
				}
		System.out.println("Detectada posible CONTRACCION_VENTRICULAR_PREMATURA en el ciclo: " + $intervaloQRS.getCiclo());	
end




rule "Detectar Contracción Ventricular Prematura"
when
    $r: Resultado (ritmoCardiaco > 0 && !enfermedad.contains(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA))
    $intervaloQRS: Intervalo(tipo == "QRS", $ciclo: ciclo, $valorQRS: duracion)
    $intervaloRR: Intervalo(tipo == "RR", ciclo == $ciclo, $valorRR: duracion)
    eval($valorQRS > 120 && $valorRR < 500)
then
    modify ($r) {
        addEnfermedad(Enfermedades.CONTRACCION_VENTRICULAR_PREMATURA);
    }
    System.out.println("Detectada posible CONTRACCION_VENTRICULAR_PREMATURA en el ciclo: " + $ciclo);	
end

*/